import os
import sys

Import("environment kashyyyk_libs")

localenv = environment.Clone()
localenv.Append(LIBS = kashyyyk_libs)

if 'bsd' in sys.platform:
  localenv.Append(CCFLAGS = " -Wno-variadic-macros ")

fltklib = ARGUMENTS.get('fltklib', '')
if fltklib == '':
    localenv.Append(LIBS=['fltk'])
else:
    localenv.Append(LINKFLAGS=[fltklib])

if sys.platform == 'darwin':
  localenv.Append(FRAMEWORKS = ["Cocoa"])
if sys.platform.startswith('linux'):
  AddOption('--disable-xinerama', dest = 'disablexinerama', default=False, help = 
  "Disable Xinerama on Linux build. This is normally not necessary or desirable unless you have a minimal X-server installed.")
  disablexinerama = GetOption('disablexinerama')
  if not disablexinerama:
    localenv.Append(LIBS = ["Xinerama"])

if ARGUMENTS.get('mingw', '0') == '1':
    localenv.Append(CPPDEFINES = "EPROTO=134")

platformenv = localenv.Clone()

kashyyyk_platform = SConscript(dirs = ["platform"], exports = "platformenv");
localenv.Append(LIBS=[kashyyyk_platform])

kashyyyk_files = ["kashyyyk.cpp",
                  "launcher.cpp",
                  "reciever.cpp",
                  "serverdatabase.cpp",
                  "servermessage.cpp",
                  "channelmessage.cpp",
                  "identityframe.cpp",
                  "message.cpp",
                  "promise.cpp",
                  "background.cpp",
                  "prefs.cpp",
                  "doubleinput.cpp",
                  "serverlist.cpp",
                  "groupeditor.cpp",
                  "server.cpp",
                  "channel.cpp",
                  "window.cpp"]

kashyyyk = localenv.Program("kashyyyk", kashyyyk_files)

if sys.platform == 'darwin':
  AppDir = os.path.join(os.getcwd(), "Kashyyyk.app")
  CntDir = os.path.join(AppDir, "Contents")
  MOSDir = os.path.join(CntDir, "MacOS")
  if not os.path.exists(AppDir):
    os.makedirs(AppDir)
  if not os.path.exists(CntDir):
    os.makedirs(CntDir)
  if not os.path.exists(MOSDir):
    os.makedirs(MOSDir)

  localenv.Install(MOSDir, kashyyyk)

